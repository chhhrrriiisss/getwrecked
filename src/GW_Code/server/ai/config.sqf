GW_AI_LIBRARY = [
	
	[
		["C_Van_01_transport_F","AKBAR","",[],0,[["Land_Device_assembled_F","[-0.000976563,-1.529297,-0.341187]",[-1.12673,0.793632,359.944],["-1","1"]],["land_wired_fence_4m_f","[0.976563,-1.511719,-1.0488892]",[0.647595,1.19629,274.491],["-1","1"]],["land_wired_fence_4m_f","[-0.992188,-1.974609,-0.892395]",[-0.59694,-1.15607,90.0109],["-1","1"]],["land_wired_fence_4m_f","[-0.949219,-1.693359,-0.0583496]",[0.659782,1.16227,270.012],["-1","1"]],["land_wired_fence_4m_f","[0.975586,-1.513672,-0.126831]",[0.704266,1.15168,270.016],["-1","1"]],["Land_Portable_generator_F","[-0.47168,-3.609375,0.0338745]",[-1.14209,0.738292,359.945],["-1","1"]],["Land_Portable_generator_F","[0.294922,-3.566406,0.0341797]",[-1.12665,0.793735,359.837],["-1","1"]],["Land_MetalBarrel_F","[-0.624023,-3.203125,-0.972656]",[-1.12743,0.792633,359.893],["-1","1"]],["Land_MetalBarrel_F","[0.0234375,-3.197266,-0.994324]",[-1.1319,0.872044,359.947],["-1","1"]],["Land_MetalBarrel_F","[0.630859,-3.154297,-1.0362549]",[-1.13104,0.873148,359.891],["-1","1"]],["Box_IND_Wps_F","[-0.0429688,-0.21875,0.683472]",[-1.1319,0.872044,359.947],["-1","1"]],["Land_CnCBarrier_stripes_F","[-0.0244141,2.117188,-1.311096]",[-1.1319,0.872044,359.947],["-1","1"]],["Land_CnCBarrier_stripes_F","[-0.0244141,2.0644531,-0.783813]",[-1.13019,0.874253,359.835],["-1","1"]],["Land_BarrelEmpty_F","[-0.487305,-3.5,-0.258972]",[-1.1319,0.872044,359.947],["-1","1"]]],[83,"SLI",11,2,[0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0],[["HORN",""],["UNFL",""],["EPLD",""],["LOCK",""],["OILS",""],["DCLK",""],["PARC",""],["TELP",""]],[]]],
		6000
	],

	[
		["B_APC_Tracked_01_AA_F","SHEELA","Pink",[],0,[["Land_FireExtinguisher_F","[1.211914,-4.869141,-1.649765]",[-1.52354,-0.0625126,0.00319374],["-1","1"]],["Land_Suitcase_F","[-0.742188,-4.912109,-1.45697]",[-1.55509,-0.0634185,0.00330886],["-1","1"]],["Land_Coil_F","[-0.0292969,-4.0234375,-2.297684]",[-0.920756,-0.0366733,0.00126529],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[2.647461,-3.865234,-3.371826]",[0.0153183,-0.354785,90.0002],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[2.649414,-0.966797,-3.333817]",[0.0159292,-0.37686,90.0002],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[1.238281,-5.396484,-3.391144]",[-0.418544,-0.017077,0.000359075],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[-1.0654297,-5.416016,-3.390091]",[-0.43811,-0.0176084,0.0003869],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[-2.539063,-3.966797,-3.372803]",[0.0181218,-0.456816,90.0003],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[-2.551758,-1.0625,-3.337265]",[0.019091,-0.49166,90.0003],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[-2.582031,1.832031,-3.301376]",[0.0195466,-0.507828,90.0003],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[-1.179688,3.267578,-3.276611]",[-0.507828,-0.0195481,0.000494553],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[1.169922,3.289063,-3.277191]",[-0.523185,-0.0199954,0.000520166],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[2.646484,1.871094,-3.296631]",[0.0219103,-0.551526,90.1123],["-1","1"]],["Land_CnCBarrier_stripes_F","[1.401367,3.482422,-2.889221]",[-0.564652,-0.0212361,0.00059281],["-1","1"]],["Land_CnCBarrier_stripes_F","[-1.398438,3.472656,-2.887573]",[-0.577034,-0.0216265,0.000615604],["-1","1"]],["Land_CnCBarrier_stripes_F","[0.0214844,3.480469,-2.887863]",[-0.599828,-0.0223707,0.000658939],["-1","1"]],["Land_CnCBarrier_stripes_F","[-2.681641,2.271484,-2.906662]",[-0.021531,0.610338,269.889],["-1","1"]],["Land_CnCBarrier_stripes_F","[-2.657227,-0.300781,-2.940018]",[-0.0242774,0.620136,270.112],["-1","1"]],["Land_CnCBarrier_stripes_F","[2.695313,2.302734,-2.904984]",[0.0230662,-0.620183,90.0004],["-1","1"]],["Land_CnCBarrier_stripes_F","[2.693359,-0.207031,-2.937927]",[-0.0237184,0.638331,270],["-1","1"]],["Land_CnCBarrier_stripes_F","[2.714844,-2.720703,-2.972168]",[0.0227649,-0.646692,89.8886],["-1","1"]],["Land_CnCBarrier_stripes_F","[2.731445,-4.236328,-2.992264]",[0.0243328,-0.654487,90.0005],["-1","1"]],["Land_CnCBarrier_stripes_F","[-2.635742,-2.75,-2.971298]",[0.0249104,-0.668841,90.0005],["-1","1"]],["Land_CnCBarrier_stripes_F","[-2.638672,-4.226563,-2.98848]",[0.0251862,-0.675397,90.0005],["-1","1"]],["Land_CnCBarrier_stripes_F","[1.541992,-5.525391,-3.00653076]",[-0.675397,-0.0251897,0.000831324],["-1","1"]],["Land_CnCBarrier_stripes_F","[-1.494141,-5.523438,-3.00485229]",[-0.681566,-0.0254585,0.000831324],["-1","1"]],["Land_CnCBarrier_stripes_F","[-0.00292969,-5.517578,-3.00558472]",[-0.687364,-0.0257175,0.00084479],["-1","1"]],["Box_Nato_AmmoVeh_F","[-0.595703,-5.121094,-2.0247192]",[-0.692809,-0.0259662,0.000857603],["-1","1"]],["Box_Nato_AmmoVeh_F","[0.803711,-5.0917969,-2.0250549]",[-0.692809,-0.0259662,0.000857603],["-1","1"]]],[83,"SLI",1,10,[0,54,0,164.946,400,546.44,4,367,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,14,0,140.906,400,295.904,2,62,0,24,0,140.906,400,295.904,2,74,0,24,0,140.906,400,295.904,2,74,0,54,0,164.946,400,546.44,4,367,0,54,0,164.946,400,546.44,4,367,0,54,0,164.946,400,546.44,4,367,0,54,0,164.946,400,546.44,4,367,0],[["HORN",""],["UNFL",""],["EPLD",""],["LOCK",""],["OILS",""],["DCLK",""],["PARC",""],["TELP",""]],[]]],
		28000
	],

	[
		["B_APC_Tracked_01_rcws_F","MAD BETTY","Leafy",[],0,[["Land_BagFence_Round_F","[1.887695,-0.382813,-1.0596008]",[-1.42856,1.50994,270.011],["-1","1"]],["Land_Pallet_vertical_F","[-1.807617,1.376953,-2.380127]",[-1.43428,1.50715,270.122],["-1","1"]],["land_mil_wallbig_4m_f","[-1.772461,0.720703,-3.661041]",[1.50892,-1.65084,90.7923],["-1","1"]],["Land_Wall_Tin_4","[1.922852,-2.800781,-1.970825]",[1.58818,-1.8098,90.1225],["-1","1"]],["land_wired_fence_4m_f","[2.0566406,0.199219,-1.829193]",[1.63373,-2.00586,90.0103],["-1","1"]],["Land_BagFence_Long_F","[-0.430664,2.0488281,-1.412262]",[2.14784,1.71947,180.032],["-1","1"]],["Land_Shoot_House_Wall_Prone_F","[0.838867,2.226563,-1.277313]",[-2.22565,-1.76116,0.092778],["-1","1"]],["Land_Wall_IndCnc_2deco_F","[0.417969,-4.587891,-3.213623]",[-2.26661,-1.8326,0.0979243],["-1","1"]],["Land_CnCBarrierMedium_F","[1.90625,-3.847656,-2.867859]",[1.86011,-2.39856,89.8996],["-1","1"]],["land_sportground_fence_f","[-1.87793,-0.449219,-3.286163]",[1.87107,-2.44539,90.1223],["-1","1"]],["land_sportground_fence_f","[1.865234,-0.621094,-3.29187]",[1.8999,-2.55683,90.0106],["-1","1"]],["Land_BagFence_Long_F","[-1.955078,-3.259766,-0.812408]",[-2.15832,2.47888,274.473],["-1","1"]],["Land_Pallet_vertical_F","[-1.920898,-3.859375,-2.230377]",[-2.10459,2.59716,272.574],["-1","1"]],["Land_BagFence_Short_F","[-1.117188,-4.861328,-1.0914307]",[-2.72555,-2.04123,0.126787],["-1","1"]],["Land_Shoot_House_Wall_Crouch_F","[1.787109,0.167969,-2.0775757]",[2.05947,-2.78609,90.0112],["-1","1"]],["Land_Shoot_House_Wall_Crouch_F","[0.855469,-4.730469,-1.574951]",[-2.83627,-2.1114,0.191808],["-1","1"]],["Land_Shoot_House_Wall_Crouch_F","[-2.0205078,-0.189453,-0.974548]",[-2.12405,2.90276,270.011],["-1","1"]],["Land_WaterBarrel_F","[-1.320313,1.433594,-1.335632]",[-2.9402,-2.13208,0.141265],["-1","1"]],["Land_WaterBarrel_F","[1.337891,1.431641,-1.33371]",[-2.98988,-2.17019,0.145819],["-1","1"]],["Land_WaterBarrel_F","[-0.0273438,1.425781,-1.333832]",[-2.28934,-2.14121,0.11304],["-1","1"]],["Box_IND_Wps_F","[0.69043,0.861328,-0.537323]",[-1.58767,-2.14954,0.0853445],["-1","1"]],["Land_FoodContainer_01_F","[-1.125,-4.71875,-1.577942]",[-0.871029,-2.14159,0.0572276],["-1","1"]],["Land_FoodContainer_01_F","[-1.395508,-4.714844,-1.577728]",[-0.893403,-2.19728,0.0597083],["-1","1"]],["Land_BarrelEmpty_grey_F","[0.110352,-1.634766,-2.40155]",[-0.199831,-2.21332,0.2009],["-1","1"]]],[83,"SLI",1,2,[0,0,0,0,0,0,0,2,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,2,0],[["HORN",""],["UNFL",""],["EPLD",""],["LOCK",""],["OILS",""],["DCLK",""],["PARC",""],["TELP",""]],[]]],
		14000
	]

];

// B_APC_Tracked_01_CRV_F - BETTY
// I_APC_tracked_03_cannon_F - BORIS
// O_APC_Tracked_02_cannon_F - GODFREY

findAITarget = {
	
	private ['_currentPos', '_currentTarget', '_skill', '_validTargets', '_currentThreat'];

	_currentPos = (ASLtoATL getPosASL _this);
	_currentTarget = objNull;
	_skill = _this getVariable ['GW_Skill', 1];
	_deployedZone = _this getVariable ['GW_Zone', nil];
	_currentZone = if (isNil "_deployedZone") then { "" } else { _deployedZone };

	// Determine current zone
	{
		_z = format['%1Zone', (_x select 0)];
		_inZone = [_currentPos, _z] call checkInZone;
		if (_inZone) exitWith { _currentZone = _z; false };
		false
	} count GW_VALID_ZONES;

	if (isNil "_currentZone") exitWith { objNull };
	_this setVariable ['GW_Zone', _currentZone];

	_targetsInZone = [_currentZone, { true }, true] call findAllInZone;	
	_validTargets = [];

	{
		_tStatus = _x getVariable ['status', []];
		_canSee = if ("cloak" in _tStatus) then { false } else {
			if (_this distance _x > (1700 + (_skill * 100))) exitWith { false }; 
			if ("nolock" in _tStatus && (random 100) > (_skill * 10)) exitWith { false };
			true 
		};

		if (isPlayer _x && (alive _x) && _x != _vehicle && _canSee) then {			
			_validTargets pushback _x;
		};
		false
	} count _targetsInZone;	

	// No valid targets? Abort
	if (count _validTargets == 0) exitWith { objNull };

	// Find the target with the highest threat
	_currentThreat = 0;
	{
		_threat = _x getVariable ['GW_Threat', 0];
		if (_threat >= _currentThreat) then {
			_currentTarget = _x;
			_currentThreat = _threat;
		};
		false
	} count _validTargets;

	_currentTarget

};

fireAtTargetAI = {
	
	private ['_v', '_t', '_s'];
	params ['_v', '_t'];
	
	_s = _v getVariable ['GW_Skill', 1];

	for "_i" from 0 to 5 + (_s * 10) step 1 do {
		_v fireAtTarget [_t, (currentWeapon _v)];
		Sleep ([0.25 - (_s / 10), 0.1, 0.25] call limitToRange);
	};

};

GW_AI_MODULE_DEFAULTS = 
[

	[
		"NTO", // Tag
		1, // Reload
		100, // Chance of use %
		{
			_t = (_this select 1) getVariable ['GW_Target', nil];
			if (isNil "_t") exitWith {};
			_inScope = ([(_this select 1), _t, 15] call checkScope);
			( !("emp" in (_this select 2)) && !("disabled" in (_this select 2)) && _inScope)
		},
		{	
			_t = (_this select 0) getVariable ['GW_Target', nil];
			_d = if (isNil "_t") then { 3 } else { ([(_t distance (_this select 0)) / 75, 1, 10] call limitToRange) };

			for "_i" from 0 to (ceil _d) step 1 do {
				_this call nitroBoost;
			};
		}
	],
	[
		"DES", // Tag
		1, // Reload
		100, // Chance of use %
		{
			_nearby = (ASLtoATL getPosASL (_this select 1)) nearEntities [["Car"], 30];			
			( !("emp" in (_this select 2)) && ({ if (isPlayer (driver _x)) exitWith {1}; false } count _nearby isEqualTo 1) )
		},
		{
			_this call empDevice;
			[(_this select 0), ['emp']] call removeVehicleStatus;

			_this spawn {
				[		
					[
						(_this select 0),
						"surprise",
						100
					],
					"playSoundAll",
					true,
					false
				] call bis_fnc_mp;

				Sleep 1;

				_this call selfDestruct;
			};

		}
	],
	[
		"MAG", // Tag
		15, // Reload
		30, // Chance of use %
		{
			(count (_this select 0) > 1)
		},
		magneticCoil
	],
	[
		"EMP", 
		10,
		75, 
		{
			(count (_this select 0) > 1)
		},
		{ _this call empDevice; [(_this select 0), ['emp']] call removeVehicleStatus; (_this select 0) doMove ((_this select 0) modelToWorld [0,(random 20), 0]); }
	],
	[
		"REP", // Tag
		30, // Reload
		80, // Chance of use %
		{
			_health = (_this select 1) getVariable ['GW_Health', 0];
			(_health <= 50)
		},
		emergencyRepair
	],
	[
		"SHD", // Tag
		30, // Reload
		80, // Chance of use %
		{
			_health = (_this select 1) getVariable ['GW_Health', 0];
			(_health <= 25)
		},
		shieldGenerator
	],	
	[
		"ELM", // Tag
		10, // Reload
		80, // Chance of use %
		{
			(count (_this select 0) > 1)
		},
		activateElectromagnet
	],
	[
		"MIN", // Tag
		10, // Reload
		80, // Chance of use %
		{
			(count (_this select 0) > 1)
		},
		dropMines
	],
	[
		"THR", // Tag
		10, // Reload
		80, // Chance of use %
		{
			(count (_this select 0) > 1)
		},
		{
			GW_CHUTE_ACTIVE = false;
			for "_i" from 0 to 7 step 1 do {			
				_this call verticalThruster;
			};	
		}
	],
	[
		"SMK", // Tag
		10, // Reload
		80, // Chance of use %
		{
			("locking" in (_this select 2) || "locked" in (_this select 2) || "fire" in (_this select 2) || "inferno" in (_this select 2))
		},
		smokeBomb
	]

];

// AI Main loop
[] spawn {

	waitUntil {

		Sleep 2;

		if (count GW_AI_ACTIVE == 0) exitWith { false };

		{
			
			_vehicle = _x;

			if (!alive _vehicle) then {
	
				// Cleanup
				_vehicle removeAllEventHandlers "Fired";
				GW_AI_ACTIVE = GW_AI_ACTIVE - [_vehicle];

				_group = _vehicle getVariable ['GW_Crew', nil];
				if (isNIl "_group") exitWith {};
				{ _x setdammage 1; } foreach units _group;
				deleteGroup _group; 

			} else {

				// Get current status effects on vehicle
				_status = _vehicle getVariable ['status', []];	
				_skill = _vehicle getVariable ['GW_Skill', 1];
				_currentPos = (ASLtoATL getPosASL _vehicle);
				_currentTarget = _vehicle getVariable ['GW_Target', objNull];
				_moduleConfig = _vehicle getVariable ['GW_Modules', []];
				_deployedZone = _vehicle getVariable ['GW_Zone', ""];

				// Vehicles with lower skill take longer to do everything (and skip ticks)
				if ((random 1) < _skill) then {

					// Avoid flipping or getting stuck
					if (alive _vehicle && !canMove _vehicle) then {
						_vehicle setPos (_vehicle modelToWorld [0,0,1]);
						_vehicle setVectorUp [0,0,1];
					};

					// If current target is dead or null, find a new target
					_currentTarget = if (isNull _currentTarget) then {
						(_vehicle call findAITarget)
					} else {
						if (alive _currentTarget) exitWith { _currentTarget };
						(_vehicle call findAITarget)
					};
					_vehicle setVariable ['GW_Target', _currentTarget];

					// If we can use modules
					_canUse = if ("emp" in _status) then { false } else { true };
					if (_canUse && !isNull _currentTarget) then {

						if (count _moduleConfig == 0) exitWith {};

						_nearby = _currentPos nearEntities [["Car", "Tank"], 60];

						{

							_reload = (_x select 1);
							_lastUse = [_x, 5, (time - _reload), [0]] call filterParam;
							_x set [5, _lastUse];

							if (time - _lastUse > _reload) then {
								_x set [5, time];

								// Check it fulfills random chance 
								_chance =  (_x select 2) * (_skill * 10);
								if ((random 100) > _chance) exitWith {};

								_condition = [_nearby, _vehicle, _status] call (_x select 3);
								if (!_condition) exitWith {};

								[_vehicle, _vehicle] call (_x select 4);
							};

							false

						} count _moduleConfig > 0;

					};

					// If we have a target, lets try move or shoot to it
					if (!isNull _currentTarget) then {

						// If target is too far, move to it's position
						if (_currentTarget distance _vehicle > 50) then {
							_vehicle doMove (_currentTarget modelToWorld [0, ([((velocity _currentTarget) distance [0,0,0]) * 3, -100, 100] call limitToRange), 0]);
						};			

						// No turret or weapon for this vehicle, abort
						if ((currentWeapon _vehicle) == "CarHorn") exitWith {};

						{ _x hideObject true; } foreach (attachedObjects _currentTarget);		
						
						// If we can fire weapons, find a target
						_canFire = if ("emp" in _status) then { false } else { true };
						if (!_canFire) exitWith {};			
						if ((random 100) < ([(_skill * 100), 30, 100] call limitToRange)) then {

							_vehicle doTarget _currentTarget;			
							_vehicle doWatch _currentTarget;	
							_vehicle doFire _currentTarget;
							_vehicle commandFire _currentTarget;			
							_targetVisibility = _vehicle aimedAtTarget [_currentTarget];
							if (_targetVisibility == 0) exitWith {};		
							
							_vName = _vehicle getVariable ['name', 'AI']; 
							_ai = driver _vehicle;
							_currentTarget setVariable['killedBy', format['%1', [name _ai, '',_vName, (typeOf _vehicle) ] ], true];
							
							[_vehicle, _currentTarget, _skill] spawn {

								_s = _this select 2;

								for "_i" from 0 to 5 + (_s * 10) step 1 do {					
									(_this select 0) fireAtTarget [_this select 1, (currentWeapon (_this select 0))];
									Sleep ([0.25 - (_s / 10), 0.1, 0.25] call limitToRange);
								};

							};			
						};

					} else {
						_vehicle doMove (_deployedZone call findLocationInZone);
					};

				};

			};

			false

		} count GW_AI_ACTIVE > 0;		

		false

	};

};
